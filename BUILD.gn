import("opencamera.gni")

group("default") {
  testonly = true
  deps = [
    ":opencamera",
    "app",
    "//third_party/onvif_srvd",
    "//third_party/openssl",
  ]

  #  deps += [ "examples" ]
  if (ave_include_test) {
    deps += [
      ":oc_unittests",
      "base:base_unittests",
      "test",
    ]
  }
}

config("library_impl_config") {
  defines = [ "OC_LIBRARY_IMPL" ]
}

config("common_inherited_config") {
  defines = []
  configs = []
  ldflags = []

  if (oc_enable_ffmpeg) {
    defines += [ "OC_FFMPEG" ]
  }

  if (oc_enable_ffmpeg_demuxer) {
    defines += [ "OC_FFMPEG_DEMUXER" ]
  }

  if (oc_enable_ffmpeg_decoder) {
    defines += [ "OC_FFMPEG_DECODER" ]
  }

  if (is_posix || is_fuchsia) {
    defines += [ "AVE_POSIX" ]
  }
  if (is_ios) {
    defines += [
      "AVE_MAC",
      "AVE_IOS",
    ]
  }
  if (is_linux || is_chromeos) {
    defines += [ "AVE_LINUX" ]
  }
  if (is_mac) {
    defines += [ "AVE_MAC" ]
  }
  if (is_fuchsia) {
    defines += [ "AVE_FUCHSIA" ]
  }
  if (is_win) {
    defines += [ "AVE_WIN" ]
  }
  if (is_android) {
    defines += [
      "AVE_LINUX",
      "AVE_ANDROID",
    ]
  }

  if (is_chromeos) {
    defines += [ "CHROMEOS" ]
  }
}

config("no_exit_time_destructors") {
  if (is_clang) {
    cflags = [ "-Wno-exit-time-destructors" ]
  }
}

config("no_global_constructors") {
  if (is_clang) {
    cflags = [ "-Wno-global-constructors" ]
  }
}

config("oc_prod_config") {
  if (is_clang) {
    cflags = [
      "-Wexit-time-destructors",
      "-Wglobal-constructors",
    ]
  }
}

config("common_config") {
  cflags = []
  cflags_c = []
  cflags_cc = []
  cflags_objc = []
  defines = []
}

oc_test("oc_unittests") {
  testonly = true
  deps = [
    "api/video/test:oc_api_video_unittests",
    "common:media_foundation_unittests",
    "media/test:oc_media_unittests",
    "test:test_main",
  ]
}

oc_library("opencamera") {
  testonly = true
  sources = []

  defines = []
  deps = []

  deps += [
    "//api",
    "//app:oc",
    "//base",
    "//common",
    "//media",
    "//modules/audio_device",
    "//onvif",
    "//rtsp",
    "//third_party/fdk-aac",
    "//third_party/inih",
    "//third_party/libyuv",
    "//third_party/onvif_srvd:onvif_srv",
    "//third_party/openh264:encoder",
    "//third_party/openssl:libssl_static",
    "//third_party/rtsp_server",
  ]
}
